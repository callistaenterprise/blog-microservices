buildscript {
    ext {
        springBootVersion = '1.3.1.RELEASE'
        springCloudVersion = 'Brixton.M4'
    }
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath 'com.bmuschko:gradle-docker-plugin:2.6.3'
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'spring-boot'
apply plugin: 'com.bmuschko.docker-remote-api'

jar {
    baseName = 'auth-server'
    version = '0.0.1-SNAPSHOT'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url "https://repo.spring.io/milestone" }
}

dependencies {
    compile('org.springframework.boot:spring-boot-starter-web') {
        exclude module: 'spring-boot-starter-tomcat'
    }
    compile("org.springframework.boot:spring-boot-starter-undertow")
    compile("org.springframework.boot:spring-boot-starter-actuator")
    compile("org.springframework.cloud:spring-cloud-starter-oauth2")
    compile("org.springframework.cloud:spring-cloud-starter-security")

    // TODO: Is this still required?
    // compile("org.springframework.boot:spring-boot-starter-security")
    // compile("org.springframework.security.oauth:spring-security-oauth2:2.0.6.RELEASE")

    testCompile("org.springframework.boot:spring-boot-starter-test") {
        // TODO: Is this still required?
        exclude(module: 'commons-logging')
    }
    // TODO: Is this still required?
    testCompile('org.apache.httpcomponents:httpclient:4.3.6') {
        exclude(module: 'commons-logging')
    }
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-starter-parent:${springCloudVersion}"
    }
}

def dockerPort = 9999

docker {
    url = 'https://192.168.99.100:2376'
    certPath = new File(System.properties['user.home'], '.docker/machine/machines/ms')
}


import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.Dockerfile

task createDockerfile(type: Dockerfile) {
    destFile = project.file('build/Dockerfile')
    from 'ofayau/ejre:8-jre'
    maintainer 'Magnus Larsson <magnus.larsson.ml@gmail.com>'
    exposePort dockerPort
    copyFile "libs/${jar.baseName}-${jar.version}.jar", "${jar.baseName}-${jar.version}.jar"

    // Regarding settings of java.security.egd, see http://wiki.apache.org/tomcat/HowTo/FasterStartUp#Entropy_Source
    entryPoint 'java','-Dspring.profiles.active=docker','-Djava.security.egd=file:/dev/./urandom','-jar',"${jar.baseName}-${jar.version}.jar"
}

task buildDockerImage(type: DockerBuildImage) {
    dependsOn createDockerfile
    inputDir = createDockerfile.destFile.parentFile
    tag = "magnuslarsson/ms-blog-${jar.baseName}"
}

eclipse {
    classpath {
        containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
        containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.9'
}
