buildscript {
    ext {
        springBootVersion = '1.3.1.RELEASE'
        springCloudVersion = 'Brixton.M4'
    }
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath 'com.bmuschko:gradle-docker-plugin:2.6.3'
    }
}

plugins {
    id "com.jfrog.bintray" version "0.4.1"
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'spring-boot'
apply plugin: 'com.bmuschko.docker-remote-api'

jar {
    baseName = 'product-composite-service'
    version = '0.0.1-SNAPSHOT'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
    maven { url "https://repo.spring.io/milestone" }
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web")
// TODO: Running with undertow gives the following startup error with springCloudVersion = 'Brixton.M4'
// - org.springframework.context.ApplicationContextException: Unable to start embedded container; nested exception is java.lang.ClassCastException: org.apache.tomcat.websocket.WsWebSocketContainer cannot be cast to io.undertow.websockets.jsr.ServerWebSocketContainer
//    {
//        exclude module: 'spring-boot-starter-tomcat'
//    }
//    compile("org.springframework.boot:spring-boot-starter-undertow")
    compile("org.springframework.boot:spring-boot-starter-actuator")

    compile("org.springframework.cloud:spring-cloud-starter-hystrix")
    compile("org.springframework.cloud:spring-cloud-starter-eureka")
//    compile("org.springframework.cloud:spring-cloud-starter-config")
//    compile("org.springframework.cloud:spring-cloud-starter-bus-amqp")
    compile("org.springframework.cloud:spring-cloud-starter-oauth2")
    compile("org.springframework.cloud:spring-cloud-starter-security")

//    compile("org.springframework.cloud:spring-cloud-netflix-hystrix-amqp:1.1.0.M4")

//    compile("io.dropwizard.metrics:metrics-core:3.1.0")
//    compile("com.readytalk:metrics3-statsd:4.1.0")

    compile("org.apache.httpcomponents:httpclient:4.2.1")
    compile("org.apache.httpcomponents:fluent-hc:4.2.1")

    compile("se.callista.microservises:util:0.0.1-SNAPSHOT")

    testCompile("org.springframework.boot:spring-boot-starter-test")
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-starter-parent:${springCloudVersion}"
    }
}

def dockerPort = 8080

docker {
    url = 'https://192.168.99.100:2376'
    certPath = new File(System.properties['user.home'], '.docker/machine/machines/ms')
}


import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.Dockerfile

task createDockerfile(type: Dockerfile) {
    destFile = project.file('build/Dockerfile')
    from 'java:8'
    maintainer 'Magnus Larsson <magnus.larsson.ml@gmail.com>'
    exposePort dockerPort
    copyFile "libs/${jar.baseName}-${jar.version}.jar", "${jar.baseName}-${jar.version}.jar"
    copyFile 'resources/main/truststore.jks', 'truststore.jks'

    // Regarding settings of java.security.egd, see http://wiki.apache.org/tomcat/HowTo/FasterStartUp#Entropy_Source
    entryPoint 'java','-Dspring.profiles.active=docker','-Djavax.net.ssl.trustStore=truststore.jks','-Djavax.net.ssl.trustStorePassword=password','-Djava.security.egd=file:/dev/./urandom','-jar',"${jar.baseName}-${jar.version}.jar"
}

task buildDockerImage(type: DockerBuildImage) {
    dependsOn createDockerfile
    inputDir = createDockerfile.destFile.parentFile
    tag = "magnuslarsson/ms-blog-${jar.baseName}"
}

eclipse {
    classpath {
        containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
        containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.9'
}
